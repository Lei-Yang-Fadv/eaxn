plugins {
    id 'java'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.boot' version '2.5.1'
    id "com.microsoft.azure.azurefunctions" version "1.7.0"
}

defaultTasks('build')

group = 'com.exan'
version = project.property('projectVersion')

repositories {
    mavenCentral()
}

compileJava {
    options.release = Integer.valueOf(project.property('javaRelease')) 
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

configurations {
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}

dependencies {
    implementation 'org.springframework.cloud:spring-cloud-function-adapter-azure:3.1.3'

    implementation 'com.azure.spring:azure-spring-boot-starter-servicebus-jms:3.7.0'
    
    implementation 'me.ehp246:auf-jms:0.5.0'

    compileOnly 'org.springframework.cloud:spring-cloud-starter-function-web:3.1.3'

    compileOnly 'org.projectlombok:lombok:1.18.18'
    annotationProcessor 'org.projectlombok:lombok:1.18.18'
    
    testCompileOnly 'org.projectlombok:lombok:1.18.18' 
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.18'
    
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

jar {
    manifest {
        attributes 'Main-Class' : 'com.eaxn.EaxnApplication'
    }
}

azurefunctions {
    appName='eaxn'
    appSettings {
        WEBSITE_RUN_FROM_PACKAGE = '1'
        FUNCTIONS_EXTENSION_VERSION = '~3'
        FUNCTIONS_WORKER_RUNTIME = 'java'
        MAIN_CLASS = 'com.eaxn.EaxnApplication'
    }
    authentication {
        type = 'azure_cli'
    }
    localDebug = "transport=dt_socket,server=y,suspend=n,address=5005"
    deployment {
        type = 'run_from_blob'
    }
}

test {
    useJUnitPlatform()
}